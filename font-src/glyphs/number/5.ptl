$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from"../../support/utils.mjs"
import [AnyCv getGrMesh] from"../../support/gr.mjs"

glyph-module

glyph-block Digits-Five : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Digits-Shared : OnumMarks ShiftDown CodeLnum CodeOnum

	define [FiveFlatStroke top xleft t2 ycurly sw] : dispiro
		widths.rhs sw
		flat (xleft + [HSwToV sw]) t2
		curl Middle t2
		archv
		g4 (RightSB - OX) ycurly
		hookend O
		g4 SB (Hook * top / CAP)

	define [FiveArcStroke top xleft t2 ycurly sw] : dispiro
		widths.rhs sw
		g4 xleft (t2 - AHook * top / CAP)
		hookstart (t2 - O)
		g4 (RightSB - OX) ycurly
		hookend O
		g4 SB (Hook * top / CAP)

	define [FiveArcStrokeMask top xleft t2 ycurly sw] : spiro-outline
		g4 (xleft + 1) (t2 - AHook * top / CAP)
		hookstart (t2 - O - 1)
		g4 (RightSB - OX - 1) ycurly
		hookend (O + 1)
		g4 (SB + 1) (Hook * top / CAP)

	define [FiveShape] : with-params [
			top bp [sw Stroke] [bbd 0] [obl 0] [zt 0]
			[bottomShape FiveArcStroke] [bottomMaskShape FiveArcStrokeMask]
		] : glyph-proc
		local t1 : top * bp * 0.8
		local t2 : top * bp
		local ycurly : YSmoothMidR t2 0 ArchDepthA ArchDepthB
		local xleft : [mix SB RightSB 0.025] + zt
		local xright : [mix RightSB SB 0.05] - (OX - O)

		local kGap : 0.144 - 0.1 * sw / t2

		include : difference
			glyph-proc
				local fiveStroke : include : bottomShape top xleft t2 ycurly sw
				local firstKnot fiveStroke.rhsKnots.0

				local oblCor : Math.hypot 1 obl
				local xVBar : firstKnot.x - oblCor * [HSwToV sw]
				local xVBarOffset : (top - firstKnot.y) * obl
				include : HBar.t [if zt SB (xVBar + xVBarOffset)] xright top sw
				include : union
					intersection [Rect (firstKnot.y + sw) firstKnot.y 0 Width] : dispiro
						flat xVBar firstKnot.y [widths.rhs.heading (sw) Upward]
						flat (xVBar + xVBarOffset) top [heading Upward]
					intersection [Rect top (firstKnot.y + sw) 0 Width] : dispiro
						flat xVBar firstKnot.y [widths.rhs.heading (oblCor * sw) Upward]
						flat (xVBar + xVBarOffset) top [heading Upward]

				if bbd : begin
					local fiveStrokeMask : bottomMaskShape top xleft t2 ycurly sw
					include : intersection fiveStrokeMask [VBar.r (RightSB - OX - bbd) 0 CAP sw]
					include : difference
						VBar.r (firstKnot.x + bbd) 0 CAP sw
						begin fiveStrokeMask
						Rect (t2 / 2) 0 0 Width

			Rect (t2 / 2 + t2 * kGap) (t2 / 2 - t2 * kGap) 0 Middle

		if (!bbd && SLAB) : include : VSerif.dr xright top [Math.min VJut ((top - t2) * 0.8)]

	define FiveConfig : SuffixCfg.weave
		object # upper-left-bar
			"upright" 0
			"oblique" (1 / 12)
		object # middle
			"arched"  FiveArcStroke
			"flat"    FiveFlatStroke

	foreach { suffix { obl bottomShape } } [pairs-of FiveConfig] : do
		create-glyph "five.lnum.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : FiveShape CAP DesignParameters.fiveBarPos (bottomShape -- bottomShape) (obl -- obl)

		create-glyph "five.onum.\(suffix)" : glyph-proc
			include : OnumMarks.p
			include : FiveShape CAP DesignParameters.fiveBarPos (bottomShape -- bottomShape) (obl -- obl)
			include : ShiftDown

		create-glyph "zhuangToneFive.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : FiveShape CAP DesignParameters.fiveBarPos (bottomShape -- bottomShape) (zt -- ((RightSB - SB) * 0.05))

		create-glyph "zhuangtonefive.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : FiveShape XH DesignParameters.fiveBarPos (bottomShape -- bottomShape) (zt -- ((RightSB - SB) * 0.05))

	select-variant 'five.lnum' [CodeLnum '5'] (follow -- 'five')
	select-variant 'five.onum' [CodeOnum '5'] (follow -- 'five')
	select-variant 'zhuangToneFive' 0x1BC
	select-variant 'zhuangtonefive' 0x1BD (follow -- 'zhuangToneFive')

	glyph-block-import Letter-Blackboard : BBS BBD
	create-glyph 'mathbb/five' 0x1D7DD : glyph-proc
		include : MarkSet.capital
		include : FiveShape CAP DesignParameters.fiveBarPos (sw -- BBS) (bbd -- BBD)
