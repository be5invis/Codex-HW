$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Armenian-Feh : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : SerifFrame

	define [FehBody df top bot ada adb] : glyph-proc
		local midy : mix bot top HBarPos
		local midyTop : midy + 0.5 * df.mvs
		local midyBot : midy - 0.5 * df.mvs
		include : dispiro
			widths.lhs df.mvs
			straight.left.start df.middle top
			archv
			flatside.ld df.leftSB midyBot top ada adb
			arcvh
			straight.right.end df.middle midyBot [heading Rightward]
		include : dispiro
			widths.rhs df.mvs
			straight.right.start df.middle midyTop [heading Rightward]
			archv
			flatside.rd df.rightSB 0 midyTop ada adb
			hookend 0 (sw -- df.mvs)
			g4 (df.leftSB + OX) Hook

	create-glyph 'armn/Feh' 0x556 : glyph-proc
		local df : include : DivFrame para.diversityM 3
		include : df.markSet.capital
		local ada : df.archDepthA ArchDepth df.mvs
		local adb : df.archDepthB ArchDepth df.mvs
		include : FehBody df CAP 0 ada adb
		include : VBar.m df.middle 0 CAP df.mvs
		if SLAB : begin
			local sf : SerifFrame.fromDf df CAP 0
			include sf.mt.right

	create-glyph 'armn/feh' 0x586 : glyph-proc
		local df : include : DivFrame para.diversityM 3
		include : df.markSet.bp
		local ada : df.archDepthA SmallArchDepth df.mvs
		local adb : df.archDepthB SmallArchDepth df.mvs
		include : FehBody df Ascender 0 ada adb
		include : VBar.m df.middle Descender Ascender df.mvs
		if SLAB : begin
			local sf : SerifFrame.fromDf df Ascender Descender
			include sf.mb.full
